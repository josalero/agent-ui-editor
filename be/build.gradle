plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

ext {
    langchain4jVersion = '1.11.0'
    langchain4jAgenticVersion = '1.11.0-beta19'
}

dependencies {
    implementation platform('dev.langchain4j:langchain4j-bom:' + langchain4jVersion)
    implementation 'dev.langchain4j:langchain4j'
    implementation 'dev.langchain4j:langchain4j-open-ai'
    implementation 'dev.langchain4j:langchain4j-agentic:' + langchain4jAgenticVersion

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jackson'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    runtimeOnly 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Integrate fe: build frontend and embed in be static resources (single server.port)
def feDistDir = project(':fe').layout.projectDirectory.dir('dist')
def beStaticDir = layout.buildDirectory.dir('resources/main/static')

tasks.register('copyFeToStatic', Copy) {
    description = 'Copy frontend production build into backend static resources'
    group = 'build'
    dependsOn ':fe:build', tasks.named('processResources')
    from feDistDir
    into beStaticDir
    onlyIf { feDistDir.asFile.exists() }
}

tasks.named('compileJava') {
    dependsOn copyFeToStatic
}
tasks.named('jar') {
    dependsOn copyFeToStatic
}
tasks.named('bootJar') {
    dependsOn copyFeToStatic
}
tasks.named('resolveMainClassName') {
    dependsOn copyFeToStatic
}
